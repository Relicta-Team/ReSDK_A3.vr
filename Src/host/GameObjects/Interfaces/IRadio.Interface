// ======================================================
// Copyright (c) 2017-2026 the ReSDK_A3 project
// sdk.relicta.ru
// ======================================================

interfaceHeader

/*
	Интерфейс радиоприёмника
	Требует определения 
	getterconst_func(isRadio,true);
	var(radioIsEnabled,true);
	var(radioType,RADIO_TYPE_WALKIETALKIE);
*/

attributeParams(hasField,"radioType" arg "radioIsEnabled" arg "radioSettings");

//! новые переменные
/*
	Переменные для радио:
	
	float - текущая частота
	string - текущая кодировка
		фактическая частота - частода_кодировка
	float - громкость приёмника. отрицательное значение указывает что радио в режиме наушника. значение нормализованное от -1 до 1
	float - дистанция слышимости объекта (при отрицательной громкости не учитывается)
	?vec3 - смещение относительно источника
	?float - мощность радио (отвечает за снижение качества звука на больших расстояниях)
*/

func(getRadioFrequencyKey)
{
	objParams();
	getSelf(radioSettings) params ["_freq","_enc"];
	[_freq,_enc] joinString "@"; //todo generate freq id by freq-enc combination
};

func(getRadioData)
{
	objParams();
	getSelf(radioSettings) params ["_freq","_enc","_vol","_hearDist","_biasPos","_wavePower"];
	private _data = [
		callSelf(getRadioFrequencyKey),
		_vol,
		_hearDist
	];
	
	private _radioType = getSelf(radioType);
	_radioType = RADIO_TYPE_STRING_TO_ENUM(_radioType);

	if !isNullVar(_radioType) then {	
		_data pushBack _radioType;
	} else {
		_data append [null];
	};
	if !isNullVar(_biasPos) then {
		_data pushBack _biasPos;
	} else {
		_data append [null];
	};
	_data
};

func(getRadioDataForInventory)
{
	objParams();
	private _data = callSelf(getRadioData);
	_data insert [0,[getSelf(pointer)]];
	_data
};

func(setRadioFrequency)
{
	objParams_1(_freq);
	_freq = clamp(_freq,0,1000);
	getSelf(radioSettings) set [0,_freq];
};

getter_func(getRadioFrequency,getSelf(radioSettings) select 0);

func(setRadioVolume)
{
	objParams_1(_vol);
	_vol = clamp(_vol,0,1);
	if (callSelf(isRadioSpeakerModeEnabled)) then {
		_vol = _vol * -1;
	};
	getSelf(radioSettings) set [2,_vol];
};

getter_func(getRadioVolume,getSelf(radioSettings) select 2);

func(setRadioSpeakerMode)
{
	objParams_1(_mode);
	
	if equals(_mode,callSelf(isRadioSpeakerModeEnabled)) exitWith {};

	private _settings = getSelf(radioSettings);
	if (_mode) then {
		_settings set [2,(_settings select 2) * -1];
	} else {
		_settings set [2,abs(_settings select 2)];
	};
};

getter_func(isRadioSpeakerModeEnabled,callSelf(getRadioVolume) < 0);

getter_func(getRadioWavePower,private _pwr = getSelf(radioSettings) select 5; ifcheck(isNullVar(_pwr),-1,_pwr));
